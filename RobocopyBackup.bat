@echo off
@rem ====================================================================================================
@rem ■機能:
@rem   robocopyを用いてClientPCのファイルバックアップ。
@rem ====================================================================================================
setlocal EnableDelayedExpansion


@rem ====================================================================================================
@rem 変数セット
@rem ====================================================================================================
@rem ----------------------------------------------------------------------------------------------------
@rem ログファイルパス
set LogFilePath=.\robocopy.log

@rem ----------------------------------------------------------------------------------------------------
@rem コピー元フォルダパス
set SrcDirPath=D:\

@rem ----------------------------------------------------------------------------------------------------
@rem コピー先フォルダパス
set DestDirPath=\\BackupComputer\ClientBk


@rem ====================================================================================================
@rem ログファイルを空にする
@rem ====================================================================================================
type nul > %LogFilePath%  2>&1


@rem ====================================================================================================
@rem "net use"接続
@rem ====================================================================================================
@rem コピー元への接続 ... 不要

@rem コピー先への接続 ... 不要


@rem ====================================================================================================
@rem robocopy実行
@rem ----------------------------------------------------------------------------------------------------
@rem <<robocopyの現在の設定の概要>>
@rem ■ファイルパラメータ(コピー対象ファイル名パターン)
@rem ・既定値(全ファイル(*.*))
@rem 
@rem ■コピーオプション:
@rem ・/EFSRAW ... 暗号化されたすべてのファイルをEFS RAWモードでコピー(暗号化ファイルも復号しないでコピー)。
@rem ・/COPY:DAT ... ファイルにコピーする情報 (既定値は /COPY:DAT)。
@rem                 ※D=データ、A=属性、T=タイムスタンプ、S=セキュリティ(NTFS ACL)、O=所有者情報、U=監査情報
@rem ・/DCOPY:T ... ディレクトリタイムスタンプをコピー。
@rem ・/MIR ... ディレクトリツリーをミラー化(/Eかつ/PURGEと同等):
@rem            ・空のディレクトリを含むサブディレクトリをコピー
@rem            ・既にコピー元に存在しないコピー先のファイル/ディレクトリを削除
@rem 
@rem ■ファイル選択オプション
@rem ・/XD ディレクトリ [ディレクトリ] ... 指定された名前/パス/ワイルドカードに一致するディレクトリを除外。
@rem       ・除外ディレクトリ名/パスの例:
@rem           $RECYCLE.BIN "System Volume Information"
@rem           bk _bk back _back bkup _bkup backup _backup old _old
@rem           _tmp _temp
@rem           release debug
@rem           .metadata runtime*configuration jre* jdk*
@rem ・以下のクラスのファイルをコピー(既定):
@rem   ・Lonely(転送元にあって転送先にない)
@rem   ・Changed(転送元先両方にある、更新日時一致、サイズが違う)
@rem   ・Newer(転送元先両方にある、転送元の更新日時が転送先より新しい)
@rem   ・Older(転送元先両方にある、転送元の更新日時が転送先より古い)
@rem ・属性の違いは見ない(既定)
@rem ・/FFT ... タイプスタンプの制度がFATファイル時間(2秒の粒度)と仮定。
@rem            ※NAS製品の中では、タイムスタンプの精度がFATなみの2秒というものがある。なのにNTFSを名乗っているものもある。
@rem              そんなディスクとNTFSのディスクの間でミラーリングをした場合に、タイムスタンプの精度の違いにより、
@rem              毎回コピーされてしまったり全然コピーされなかったりとうまく動かないことがある。
@rem              そんなことが起きないように、/FFT オプションを使う。
@rem 
@rem ■再試行オプション
@rem ・/R:3 ... 失敗したコピーに対する最大再試行数=3回
@rem            ※既定値だと1,000,000回なので、最悪な場合は処理が凍ってしまう。
@rem ・/W:20 ... 再試行と再試行の間の待機時間=20秒
@rem 
@rem ■ログオプション
@rem ・/FP ... 出力にファイルの完全なパス名を含める。
@rem ・/NS ... サイズなし - ファイルサイズをログに記録しない。
@rem ・/NDL ... ディレクトリなし - ディレクトリ名をログに記録しない。
@rem            ※ログファイルをコンパクトにできる。
@rem ・/NP ... 進行状況なし - コピーの完了率を表示しない。
@rem           ※大きなファイルの場合、このオプションが無いと1%刻みでログを出力してしまうので、
@rem             バッチ実行時には必ずつけるべき。
@rem ・/LOG+:%LogFilePath% ... ログファイルに状態を出力(既存のログに追加)。ログファイルパスは%LogFilePath%で指定。
@rem 
@rem ■ジョブオプション
@rem ・なし
@rem 
@rem ※注意
@rem ・Windows Vista以降で/ZBや/COPYALLを使用する場合は、「管理者として実行」権限で実行する必要がある。
@rem ・/EFSRAWはWindows Server 2003以前には存在しない。
@rem ====================================================================================================
cmd /c call robocopy %SrcDirPath% %DestDirPath% ^
                     /EFSRAW /COPY:DAT /DCOPY:T /MIR ^
                     /XD ^
                       $RECYCLE.BIN "System Volume Information" ^
                       bk _bk back _back bkup _bkup backup _backup old _old ^
                       _tmp _temp ^
                       release *debug* ^
                       .metadata runtime*configuration jre* jdk* ^
                       SQLServer ^
                     /FFT ^
                     /R:3 /W:20 ^
                     /FP /NS /NDL /NP /LOG+:%LogFilePath%


@rem ====================================================================================================
@rem コピー元がドライブの場合、コピー先フォルダにシステムファイル属性と隠しファイル属性が付与されるので、その属性を解除する。
@rem ※attribコマンドは、引数パスの末尾が"\"で終わっていると認識してくれないので、"\"で終わらないパスを指定する。
@rem ====================================================================================================
echo.>> %LogFilePath%  2>&1
echo cmd /c call attrib -S -H %DestDirPath% >> %LogFilePath%  2>&1
cmd /c call attrib -S -H %DestDirPath% >> %LogFilePath%  2>&1


@rem ====================================================================================================
@rem "net use"接続解除
@rem ※タスクスケジューラから実行するバッチ内で"net use"接続した場合は、バッチ内で最後に接続解除しておくのが無難。
@rem ====================================================================================================
@rem コピー元への接続解除 ... 不要

@rem コピー先への接続解除 ... 不要


endlocal
exit /b
